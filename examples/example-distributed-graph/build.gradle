group = rootProject.group
version = rootProject.version

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: "com.github.johnrengelman.shadow"

ext {
    mainClass = "io.amient.affinity.example.ExampleGraphMain"
    mainScript = "example-graph-api"
    kafkaMinorVersion = "0.11"
    sparkVersion = '2.0.2'
    baseSparkVersion = sparkVersion.split("\\.\\d+\$")[0]
}

dependencies {
    compile project(':core')
    compile project(':kafka:kafka_' + kafkaMinorVersion + ':storage-kafka')
    compile group: "ch.qos.logback", name: "logback-classic", version: "1.2.3"
    compile group: "ch.qos.logback", name: "logback-core", version: "1.2.3"
    compile 'org.scala-lang:scala-library:' + rootProject.scalaVersion
    testCompile project(':ws-client')
    testCompile project(':kafka:kafka_' + kafkaMinorVersion + ':test-util-kafka')
    testCompile project(':spark:spark_' + baseSparkVersion)
    testCompile group: 'org.apache.spark', name: 'spark-core_' + baseScalaVersion, version: sparkVersion
    testCompile group: 'org.apache.avro', name: 'avro-mapred', version: avroVersion, classifier: 'hadoop2'
}


build.dependsOn shadowJar
shadowJar {
    from { project.configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes(
            'Main-Class': mainClass,
            'version': project.version,
        )
    }
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer) {
        resource = 'reference.conf'
    }
}

task createStartScripts(type: CreateStartScripts) {
    outputDir = file("build/scripts")
    mainClassName = mainClass
    applicationName = mainScript
    classpath = shadowJar.outputs.files
}

build.dependsOn createStartScripts


