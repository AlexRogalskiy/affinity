group = 'io.amient.affinity'
version = '0.2.0-SNAPSHOT'

ext {
    javaVersion = '1.8'
    scalaVersion = '2.11.7'
    baseScalaVersion = scalaVersion.split("\\.\\d+\$")[0]
    akkaVersion = '2.4.10'
    zkClientVersion = '0.9'
    avroVersion = '1.8.1'
    jacksonVersion = '2.7.5'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}



allprojects {

    apply plugin: 'idea'
    apply plugin: 'maven'

    version = rootProject.version

    plugins.withType(JavaPlugin).whenPluginAdded {

        sourceCompatibility = rootProject.ext.javaVersion

        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                url "http://packages.confluent.io/maven/"
            }
        }

        configurations {
            provided
            testCompile.extendsFrom provided
        }

        sourceSets {
            main {
                compileClasspath += configurations.provided
            }
        }

        idea {
            module {
                scopes.PROVIDED.plus += [configurations.provided]
            }
        }

        test.systemProperties 'property': 'value'
    }

    plugins.withType(ScalaPlugin).whenPluginAdded {

        task scalaTest(dependsOn: ['testClasses'], type: JavaExec) {
            main = 'org.scalatest.tools.Runner'
            args = ['-R', 'build/classes/test', '-o']
            classpath = sourceSets.test.runtimeClasspath
        }
        test.dependsOn scalaTest

        dependencies {
            compile 'org.scala-lang:scala-library:' + scalaVersion
            testCompile group: 'org.scalatest', name: 'scalatest_' + baseScalaVersion, version: '2.2.6'
            testCompile group: 'org.scalacheck', name: 'scalacheck_' + baseScalaVersion, version: '1.12.5'
        }
    }
}
