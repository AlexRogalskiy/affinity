group = 'io.amient.affinity'
version = '0.3.0-SNAPSHOT'

ext {
    javaVersion = '1.8'
    junitVersion = '4.12'
    scalaVersion = '2.11.8'
    scalaTestVersion = '2.2.6'
    scalaCheckVersion = '1.12.5'
    baseScalaVersion = scalaVersion.split("\\.\\d+\$")[0]
    typesafeConfigVersion = '1.3.0'
    akkaVersion = '2.4.20'
    akkaHttpVersion = '10.0.10'
    zkClientVersion = '0.8'
    zookeeperVersion = '3.4.9' // zookeeper version is interdependent with kafka version
    avroVersion = '1.8.2' // avro version provides also jackson transiently
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

allprojects {

    apply plugin: 'idea'

    version = rootProject.version

    buildscript {
        repositories {
            mavenCentral()
        }
        dependencies {
            classpath 'com.github.rholder:gradle-one-jar:1.0.4'
        }
    }

    plugins.withType(JavaPlugin).whenPluginAdded {

        sourceCompatibility = rootProject.ext.javaVersion

        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                url "http://packages.confluent.io/maven/"
            }
        }

        configurations {
            provided
            testCompile.extendsFrom provided
        }

        sourceSets {
            main.compileClasspath += configurations.provided
            test.compileClasspath += configurations.provided
            test.runtimeClasspath += configurations.provided
        }

        idea {
            module {
                scopes.PROVIDED.plus += [configurations.provided]
            }
        }

        test.systemProperties 'property': 'value'

        dependencies {
            testCompile group: 'junit', name: 'junit', version: junitVersion
        }

    }

    plugins.withType(ScalaPlugin).whenPluginAdded {

        task scalaTest(dependsOn: ['testClasses'], type: JavaExec) {
            main = 'org.scalatest.tools.Runner'
            args = ['-R', 'build/classes/test', '-o']
            classpath = sourceSets.test.runtimeClasspath
        }
        test.dependsOn scalaTest

        dependencies {
            testCompile group: 'org.scalatest', name: 'scalatest_' + baseScalaVersion, version: scalaTestVersion
            testCompile group: 'org.scalacheck', name: 'scalacheck_' + baseScalaVersion, version: '1.12.5'
        }
    }
}


//monkey patch for IDEA vs. Gradle 3.+
ScalaCompileOptions.metaClass.daemonServer = true
ScalaCompileOptions.metaClass.fork = true
ScalaCompileOptions.metaClass.useAnt = false
ScalaCompileOptions.metaClass.useCompileDaemon = false
