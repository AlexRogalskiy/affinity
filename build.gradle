version = '0.0.1-SNAPSHOT'

ext {
    javaVersion = '1.8'
    scalaVersion = '2.11.7'
    baseScalaVersion = scalaVersion.split("\\.\\d+\$")[0]
    akkaVersion = '2.4.10'
    zkClientVersion = '0.9'
    rocksDbVersion = '4.9.0'
    kafkaClientVersion = '0.10.0.1'
    avroVersion = '1.8.1'
    jacksonVersion = '2.7.5'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

allprojects {

    apply plugin: 'idea'

    version = rootProject.version

    plugins.withType(JavaPlugin).whenPluginAdded {

        sourceCompatibility = rootProject.ext.javaVersion

        repositories {
            mavenLocal()
            mavenCentral()
        }

        configurations {
            provided
            testCompile.extendsFrom provided
        }

        sourceSets {
            main {
                compileClasspath += [configurations.provided]
            }
        }

        idea {
            module {
                scopes.PROVIDED.plus += [configurations.provided]
            }
        }

        task scalaTest(dependsOn: ['testClasses'], type: JavaExec) {
            main = 'org.scalatest.tools.Runner'
            args = ['-R', 'build/classes/test', '-o']
            classpath = sourceSets.test.runtimeClasspath
        }
        test.dependsOn scalaTest
        test.systemProperties 'property': 'value'

        dependencies {
            //    compile group: 'com.google.inject', name: 'guice', version: 3.0
            //    compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.6'
            //    compile group: 'org.slf4j', name:'slf4j-log4j12', version: '1.7.6'
        }
    }

    plugins.withType(ScalaPlugin).whenPluginAdded {

        dependencies {
            compile 'org.scala-lang:scala-library:' + scalaVersion
            testCompile group: 'org.scalatest', name: 'scalatest_' + baseScalaVersion, version: '2.2.6'
            testCompile group: 'org.scalacheck', name: 'scalacheck_' + baseScalaVersion, version: '1.12.5'
        }
    }
}

